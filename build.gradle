/*
 * Copyright 2013-2019 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.gradle.build-scan' version '2.0.2'
    id 'org.kordamp.gradle.project' version '0.15.1' apply false
    id 'com.gradle.plugin-publish' version '0.9.10' apply false
    id 'com.github.kt3k.coveralls' version '2.8.2' apply false
    id 'net.ossindex.audit' version '0.1.1' apply false
    id 'org.kordamp.gradle.jdeps' version '0.5.0' apply false
    id 'fi.linuxbox.download.worker' version '0.3' apply false
    id 'org.ysb33r.ivypot' version '0.9' apply false
    id 'org.ysb33r.os' version '0.9' apply false
    id 'org.ysb33r.cloudci' version '2.5' apply false
    id 'org.ysb33r.cloudci.appveyor.testreporter' version '2.5' apply false
    id 'org.ysb33r.gradletest' version '2.0-rc.4' apply false
    id 'idea'
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'

    buildFinished { buildResult ->
        buildScanPublished { scan ->
            ['curl', '-s', '-d', "message=asciidoctor-gradle-plugin build scan: ${scan.buildScanUri}", 'https://webhooks.gitter.im/e/6ba348eef26407adc22a'].execute()
        }
    }
}

import org.gradle.util.GradleVersion

apply from: 'gradle/project-config.gradle'

allprojects {
    ext {
        offlineRepoRoot = file("${project(':testfixtures-offline-repo').buildDir}/repo")
        offlineRepoBinariesRoot = file("${offlineRepoRoot}/binaries")
        offlineRepoDescriptionFile = file("${offlineRepoRoot}/repositories.gradle")

        withoutIntegrationTests = [
            'asciidoctor-gradle-jvm-gems',
            'kindlegen-gradle'
        ]

        withoutCompatibilityTests = [
            'kindlegen-gradle'
        ]

        withoutCodeCoverage = [
            'asciidoctorj-gradle-base'
        ]
    }
}

subprojects {

    apply plugin: 'org.ysb33r.os'

    if (project.name != 'testfixtures-offline-repo') {
        apply plugin: 'groovy'
        apply plugin: 'idea'
        apply plugin: 'org.ysb33r.cloudci.appveyor.testreporter'
        apply plugin: 'org.kordamp.gradle.jar'
        apply plugin: 'org.kordamp.gradle.build-info'

        if (!project.name.startsWith('testfixture')) {
            apply plugin: 'net.ossindex.audit'
            apply plugin: 'org.kordamp.gradle.jdeps'
            apply plugin: 'org.kordamp.gradle.groovydoc'

            if (!(project.name in withoutCodeCoverage)) {
                apply plugin: 'jacoco'

                jacoco {
                    if (JavaVersion.current().isJava11Compatible() && GradleVersion.version(toolVersion) < GradleVersion.version('0.8.2')) {
                        toolVersion = '0.8.2'
                    }
                }
            }
        }

        apply from: "${rootProject.projectDir}/gradle/standard-repositories.gradle"
        apply from: "${rootProject.projectDir}/gradle/code-quality.gradle"

        if (!project.name.startsWith('testfixture')) {

            if (!(project.name in withoutIntegrationTests)) {
                apply from: "${rootProject.projectDir}/gradle/integration-tests.gradle"
            }

            apply from: "${rootProject.projectDir}/gradle/credentials.gradle"
            apply from: "${rootProject.projectDir}/gradle/publishing.gradle"
            apply from: "${rootProject.projectDir}/gradle/gradle-plugin-dependencies.gradle"
            apply from: "${rootProject.projectDir}/gradle/gradle-plugin-documentation.gradle"

            if (project.name.startsWith('asciidoctor-gradle-jvm')) {
                apply from: "${rootProject.projectDir}/gradle/asciidoctorj-versions.gradle"
                apply from: "${rootProject.projectDir}/gradle/jruby-versions.gradle"
                apply from: "${rootProject.projectDir}/gradle/ci.gradle"
            }

//            dependencyUpdates.resolutionStrategy = {
//                componentSelection { rules ->
//                    rules.all { selection ->
//                        boolean rejected = ['beta', 'rc', 'cr', 'm'].any { qualifier ->
//                            selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
//                        }
//                        if (rejected) {
//                            selection.reject('Release candidate')
//                        }
//                    }
//                }
//            }

            audit {
                failOnError = false
            }

            if (!(project.name in withoutCompatibilityTests)) {
                apply from: "${rootProject.projectDir}/gradle/compatibility-tests.gradle"
            }
        }

        compileGroovy {
            sourceCompatibility = project.sourceCompatibility
            targetCompatibility = project.targetCompatibility
        }


        project.tasks.withType(Test) {
            testLogging {
                afterSuite { desc, result ->
                    if (!desc.parent) {
                        println "Test results ${project.name}: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
                    }
                }
            }
        }
    }
}

apply from: 'gradle/coveralls.gradle'

//idea {
//    extended {
//        workspace.junit.tasks = ['classes', 'pluginUnderTestMetadata']
//    }
//}

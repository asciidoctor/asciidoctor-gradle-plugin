= Asciidoctor Gradle Plugin
Andres Almiray <https://github.com/aalmiray[@aalmiray]>
:version: 1.5.0
:asciidoc-url: http://asciidoc.org
:asciidoctor-url: http://asciidoctor.org
:issues: https://github.com/asciidoctor/asciidoctor-maven-plugin/issues
:gradle-url: http://gradle.org/
:asciidoctor-maven-plugin: https://github.com/asciidoctor/asciidoctor-maven-plugin
:lightguard: https://github.com/LightGuard
:asciidoctorj: https://github.com/asciidoctor/asciidoctorj
:lordofthejars: https://github.com/lordofthejars
:asciidoctor-docs: http://asciidoctor.org/docs/
:project-name: asciidoctor-gradle-plugin

image:http://img.shields.io/travis/asciidoctor/{project-name}/master.svg["Build Status", link="https://travis-ci.org/asciidoctor/{project-name}"]
image:http://img.shields.io/coveralls/asciidoctor/{project-name}/master.svg["Coverage Status", link="https://coveralls.io/r/asciidoctor/{project-name}"]
image:http://img.shields.io/:semver-{version}-blue.svg["Semantic Versioning", link="http://semver.org"]
image:http://img.shields.io/badge/download-latest-bb00bb.svg[link="https://bintray.com/aalmiray/asciidoctor/{project-name}/_latestVersion"]

The {doctitle} is the official means of using {asciidoctor-url}[Asciidoctor] to render all your {asciidoc-url}[AsciiDoc] documentation using {gradle-url}[Gradle].

This is a port of the {asciidoctor-maven-plugin}[Asciidoctor Maven Plugin] project founded by {lightguard}[@LightGuard]. Relies on {asciidoctorj}[AsciidoctorJ] founded by {lordofthejars}[@lordofthejars].

== Installation

Use the following snippet inside a Gradle build file:

.build.gradle
[source,groovy]
[subs="attributes"]
----
buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:{project-name}:{version}'
    }
}

apply plugin: 'org.asciidoctor.gradle.asciidoctor'
----

== Usage

The plugin adds a new task named `asciidoctor`. This task exposes a few properties as part of its configuration

[horizontal]
sourceDir:: where the asciidoc sources are. Type: File. Default: `src/asciidoc`.
sourceDocumentNames:: an override to process multiple source files, which may be a subset of all
files available in [x-]`${sourceDir}`. Type: FileCollection. Defaults to all files in [x-]`${sourceDir}`.
outputDir:: where generated docs go. Type: File. Default: `$buildDir/asciidoc`.
backends:: the backends to use. Type: Set<String>. Default: [`html5`].
options:: a Map specifying different options that can be sent to Asciidoctor.
logDocuments:: a boolean specifying if documents being processed should be logged on console. Type: boolean. Default: `false`.
gemPath:: one or more gem installation directories (separated by the system path separator). Type: String. Default: `null`
requires:: a set of Ruby modules to be included. Type: Set<String>. Default: empty.

Sources may have any of the following extensions in order to be discovered

 * .asciidoc
 * .adoc
 * .asc
 * .ad

The following attributes are automatically set by the `Asciidoctor` task

 * project-name : matches `$project.name`
 * project-version: matches `$project.version` (if defined). Empty String value if undefined
 * project-group: matches `$project.group` (if defined). Empty String value if undefined

These attributes may be overridden by explicit user input.

== Compatibility With Previous Releases

=== Task Properties

The following properties have been marked as deprecated. Developers are encouraged to migrate ASAP to the alternate
properties.

[horizontal]
sourceDocumentName:: an override to process a single source file. Use `sourceDocumentNames` instead.
backend:: the backend to use. Use `backends` instead.

=== Behavior

Files specified in `sourceDocumentNames` must be relative to `sourceDir` and fully contained in `sourceDir`, in other words,
it's no longer possible to process documents placed outside of the project's sources.

== Configuration

This plugin uses `asciidoctorj-1.5.0` by default, however you can change this by
defining a value on the +asciidoctorj+ extension, like so

.build.gradle
[source,groovy]
----
asciidoctorj {
    version = '1.6.0.SNAPSHOT'
}
----

Do not forgte to add an entry to the `repositories` block pointint to Maven local if you'd like to run a local version
of Asciidoctor (such as an snapshot build for testing bleeding edge features). The following snippet is all you need

.build.gradle
[source,groovy]
----
repositories {
    mavenLocal() // <1>
    jcenter()    // <2>
}

asciidoctorj {
    version = '1.6.0.MY_SNAPSHOT'
}
----
<1> resolves artifacts in your local Maven repository
<2> resolves artifacts in Bintray's jcenter (where all other dependencies are found)

The following options may be set using the task's `options` property

 * header_footer - boolean
 * template_dirs - List<String>
 * template_engine - String
 * doctype - String
 * attributes - Map

Any key/values set on `attributes` are sent as is to Asciidoctor. You may use this Map to specify
an stylesheet for example. The following snippet shows a sample configuration defining attributes

.build.gradle
[source,groovy]
----
asciidoctor { <1>
    outputDir = new File("$buildDir/docs")
    options = [
        doctype: 'book',
        attributes: [
            'source-highlighter': 'coderay',
            toc                 : '',
            idprefix            : '',
            idseparator         : '-'
        ]
    ]
}
----
<1> append below the line: `apply plugin: 'org.asciidoctor.gradle.asciidotor'`

You may need to include extra content into the head of the exported document.
For example, you might want to include jQuery inside the `<head>` element of the HTML export.
To do so, first create a docinfo file `src/asciidoc/docinfo.html` containing the content to include, in this case the `<script>` tag to load jQuery.

.src/asciidoc/docinfo.html
[source,html]
----
<script src="//cdnjs.cloudflare.com/ajax/libs/jquery/2.0.3/jquery.js"></script>
----

Then, add the `docinfo1` attribute to the attributes list in the previous example:

.build.gradle
[source,groovy]
----
attributes: [
    // ...
    docinfo1: '',
    // ...
]
----

The value of `attributes` my be specified as a Map, List, Array or String, for example the following Map definition

.build.gradle
[source,groovy]
----
options = [
    attributes: [
        toc: 'right',
        'source-highlighter': 'coderay',
        'toc-title': 'Table of Contents'
    ]
]
----

may be rewritten in List/Array form as follows

.build.gradle
[source,groovy]
----
options = [
    attributes: [
        'toc=right',
        'source-highlighter=coderay',
        'toc-title=Table of Contents'
    ]
]
----

or in String form like so

.build.gradle
[source,groovy]
----
options = [
    attributes: 'toc=right source-highlighter=coderay toc-title=Table\\ of\\ Contents'
]
----

IMPORTANT: Do not forget to transform Groovy strings into Strings (by explicitly invoking `.toString()` on them) when
used as option values, otherwise the Ruby runtime will throw an exception.

Notice how spaces are escaped in the last key/value pair.

Refer to the {asciidoctor-docs}[Asciidoctor documentation] to learn more about these options and attributes.

== Custom Extensions

Starting with version 1.5.0 you'll be able to write your own Asciidoctor extensions in Groovy, or any other JVM language
for that matter. There are several options for you to make it happen

=== External Library

This is the most versatile option, as it allows you to reuse the same extension in different projects. An external library
is just like any other Java/Groovy project. You simply define a dependency using the `asciidoctor` configuration.

.build.gradle
[source,groovy]
----
dependencies {
    asciidoctor 'com.acme:asciidoctor-extensions:x.y.z'
}
----

=== Project Dependency

The next option is to host the extension project in a multi-project build. This allows for a much quicker development cycle
as you don't have to publish the jar to a repository every time you make adjustments to the code. Take for example the
following setup

[source]
----
.
├── build.gradle
├── core
│   ├── build.gradle
│   └── src
│       ├── asciidoc
│       │   └── index.adoc
│       └── main
│           └── java
├── extension
│   ├── build.gradle
│   └── src
│       └── main
│           ├── groovy
│           │   └── org
│           │       └── asciidoctor
│           │           └── example
│           │               ├── ExampleExtensionRegistry.groovy
│           │               └── YellBlock.groovy
│           └── resources
│               └── META-INF
│                   └── services
│                       └── org.asciidoctor.extension.spi.ExtensionRegistry
└── settings.gradle
----

The `extension` project is a sibling for `core`. The build file for the latter looks like this

.build.gradle
[source,groovy]
[subs="attributes"]
----
buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:{version}'
    }
}

apply plugin: 'org.asciidoctor.gradle.asciidoctor'

repositories {
    jcenter()
}

dependencies {
    asciidoctor project(':extension')
}
----

=== Inline extensions

The next option is to define extensions directly in the build script.
This approach is based on the project asciidoctorj-groovy-dsl that allows to define Asciidoctor extensions in Groovy.
An extension is registered via the `extensions` element.

.build.gradle
[source,groovy]
----
asciidoctor {
    extensions {
        block(name: "BIG", contexts: [":paragraph"]) {
            parent, reader, attributes ->
            def upperLines = reader.readLines()
                .collect {it.toUpperCase()}
                .inject("") {a, b -> a + '\n' + b}

            createBlock(parent, "paragraph", [upperLines], attributes, [:])
        }
    }
}
----

http://github.com/asciidoctor/asciidoctorj-groovy-dsl contains a description of the DSL itself.

Groovy extensions can also be included as files.

.build.gradle
[source,groovy]
----
asciidoctor {
    extensions new File('big.groovy')
}
----

.big.groovy
[source,groovy]
----
block(name: "BIG", contexts: [":paragraph"]) {
    parent, reader, attributes ->
    def upperLines = reader.readLines()
        .collect {it.toUpperCase()}
        .inject("") {a, b -> a + '\n' + b}

    createBlock(parent, "paragraph", [upperLines], attributes, [:])
}
----


=== Build Dependency

The last option is to move the `extension` project into Gradle's `buildSrc` directory. There are no additional dependencies
that must defined on the consuming projects, as the extension will be automatically picked up by the `asciidoctor` task,
as the compiled extension is already in the task's classpath.

apply plugin: 'org.ysb33r.gradletest'

pluginManager.withPlugin('org.ysb33r.cloudci') {
    ci {
        travisci {
            gradleTest {
                if (JavaVersion.current().java11) {
                    versions '4.9', '5.5.1'
                } else {
                    versions '4.9', '6.0.1'
                }
            }
        }
        appveyor {
            gradleTest {
                versions '4.9', '5.0', '5.6.4', '6.0.1'
            }
        }
        no_ci {
            gradleTest {
                if (JavaVersion.current().java12) {
                    versions '5.4.1', '6.0.1'
                } else {
                    versions '4.9', '5.0', '5.5.1', '6.0.1'
                }
            }
        }
    }
}

gradleTest {
    if (tasks.findByName('intTest')) {
        mustRunAfter 'intTest'
    }
    enabled = !gradle.startParameter.offline

    dependsOn ':testfixtures-offline-repo:buildOfflineRepositories'
    systemProperties OFFLINE_REPO: offlineRepoRoot.absolutePath
}

licenseGradleTest.enabled = false

pluginManager.withPlugin('jacoco') {
    jacocoTestReport {
        executionData gradleTest
        mustRunAfter gradleTest
    }
}
